// =================================
// Copyright (c) 2018 Seppo Laakko
// Distributed under the MIT license
// =================================

[hpp]#include <gendoc/parsing/Grammar.hpp>
[hpp]#include <gendoc/parsing/ParsingDomain.hpp>
[cpp]#include <gendoc/syntax/Element.hpp>
[cpp]#include <gendoc/syntax/Rule.hpp>

namespace gendoc.syntax
{
    [hpp]using namespace gendoc::parsing;

    grammar GrammarGrammar
    {
        using Element.Identifier;
        using Element.QualifiedId;
        using Element.Signature;
        using Element.RuleLink;
        using Rule.Rule;
        using stdlib.char;

        Grammar(gendoc::parsing::Scope* enclosingScope, ParsingDomain* parsingDomain): gendoc::parsing::Grammar*
                    ::= keyword("grammar") Identifier:name!{ value = new Grammar(name, enclosingScope, parsingDomain); value->SetSpan(span); value->SetNs(enclosingScope->Ns()); } 
                        '{'! GrammarContent(value) '}'!
                    ;
                    
        GrammarContent(gendoc::parsing::Grammar* grammar)
                    ::= 
                    (   StartClause(grammar)
                    |   SkipClause(grammar)
                    |   RuleLink(grammar)
                    |   Rule(grammar->GetScope()){ grammar->AddRule(Rule); }
                    )*
                    ;

        StartClause(gendoc::parsing::Grammar* grammar)
                    ::= keyword("start") Identifier:strt! ';'{ grammar->SetStartRuleName(strt); }
                    ;

        SkipClause(gendoc::parsing::Grammar* grammar) 
                    ::= keyword("skip") QualifiedId:skp! ';'{ grammar->SetSkipRuleName(skp); }
                    ;
    }
}
