// =================================
// Copyright (c) 2018 Seppo Laakko
// Distributed under the MIT license
// =================================

[cpp]#include <gendoc/parsing/Namespace.hpp>
[cpp]#include <gendoc/parsing/Grammar.hpp>
[cpp]#include <gendoc/code/Declaration.hpp>
[hpp]#include <gendoc/syntax/ParserFileContent.hpp>
[cpp]#include <gendoc/syntax/Element.hpp>
[cpp]#include <gendoc/syntax/Grammar.hpp>

namespace gendoc.syntax
{
    grammar ParserFile
    {
        using stdlib.spaces_and_comments;
        using stdlib.newline;
        using stdlib.qualified_id;
        using GrammarGrammar.Grammar;
        using gendoc.code.Declaration.UsingDeclaration;
        using gendoc.code.Declaration.UsingDirective;
        using gendoc.code.Declaration.NamespaceAliasDefinition;

        skip spaces_and_comments;

        ParserFile(int id_, gendoc::parsing::ParsingDomain* parsingDomain_): ParserFileContent*
                    ::= empty{ value = new ParserFileContent(id_, parsingDomain_); value->SetFilePath(fileName); }
                        IncludeDirectives(value)?
                        NamespaceContent(value, parsingDomain_)
                    ;

        IncludeDirectives(ParserFileContent* parserFileContent)
                    ::= IncludeDirective(parserFileContent)+
                    ;

        IncludeDirective(ParserFileContent* parserFileContent)
                    ::= FileAttribute? '#' keyword("include") token(IncludeFileName [^\r\n]* newline){ parserFileContent->AddIncludeDirective(ToUtf8(IncludeFileName), ToUtf8(FileAttribute)); }
                    ;

        FileAttribute: std::u32string
                    ::= '[' ("cpp" | "hpp"){ value = std::u32string(matchBegin, matchEnd); } ']'
                    ;

        IncludeFileName: std::u32string
                    ::= token('<' [^>\r\n]+ '>'){ value = std::u32string(matchBegin, matchEnd); }
                    ;

        NamespaceContent(ParserFileContent* parserFileContent, ParsingDomain* parsingDomain)
                    ::= 
                    (   
                        FileAttribute? 
                        (   UsingDeclaration{ UsingDeclaration->SetFileAttr(FileAttribute); parserFileContent->AddUsingObject(UsingDeclaration); }
                        |   UsingDirective{ UsingDirective->SetFileAttr(FileAttribute); parserFileContent->AddUsingObject(UsingDirective); }
                        |   NamespaceAliasDefinition{ NamespaceAliasDefinition->SetFileAttr(FileAttribute); parserFileContent->AddUsingObject(NamespaceAliasDefinition); }
                        )
                    |   Grammar(parserFileContent->CurrentScope(), parsingDomain){ parserFileContent->AddGrammar(Grammar); }
                    |   Namespace(parserFileContent, parsingDomain)
                    )*
                    ;

        Namespace(ParserFileContent* parserFileContent, ParsingDomain* parsingDomain)
                    ::= keyword("namespace") qualified_id:ns!{ parserFileContent->BeginNamespace(ns); }
                        '{'!
                        NamespaceContent(parserFileContent, parsingDomain)
                        '}'!{ parserFileContent->EndNamespace(); }
                    ;
    }
}
