// =================================
// Copyright (c) 2018 Seppo Laakko
// Distributed under the MIT license
// =================================

[cpp]#include <gendoc/code/Expression.hpp>
[cpp]#include <gendoc/code/Declaration.hpp>
[cpp]#include <gendoc/code/Declarator.hpp>
[cpp]#include <gendoc/code/Identifier.hpp>
[hpp]#include <gendoc/codedom/Statement.hpp>
[cpp]#include <gendoc/codedom/Type.hpp>

namespace gendoc.code
{
    [cpp]using namespace gendoc::codedom;
    
    grammar Statement
    {
        using Expression.Expression;
        using Expression.ConstantExpression;
        using Expression.AssignmentExpression;
        using Declarator.TypeId;
        using Declarator.Declarator;
        using Declarator.TypeSpecifierSeq;
        using Declarator.AbstractDeclarator;
        using Identifier.Identifier;
        using Declaration.SimpleDeclaration;
        using Declaration.BlockDeclaration;
        using stdlib.spaces_and_comments;
        skip spaces_and_comments;
        start CompoundStatement;
        
        Statement: gendoc::codedom::Statement*
                    ::= LabeledStatement{ value = LabeledStatement; }
                    |   EmptyStatement{ value = EmptyStatement; }
                    |   CompoundStatement{ value = CompoundStatement; }
                    |   SelectionStatement{ value = SelectionStatement; }
                    |   IterationStatement{ value = IterationStatement; }
                    |   JumpStatement{ value = JumpStatement; }
                    |   DeclarationStatement{ value = DeclarationStatement; }
                    |   TryStatement{ value = TryStatement; }
                    |   ExpressionStatement{ value = ExpressionStatement; }
                    ;
        
        LabeledStatement: gendoc::codedom::Statement*
                    ::= (Label (':' - "::") Statement:s1!){ value = new LabeledStatement(Label, s1); }
                    |   (keyword("case") ConstantExpression! ':'! Statement:s2!){ value = new CaseStatement(ConstantExpression, s2); }
                    |   (keyword("default") ':'! Statement:s3!){ value = new DefaultStatement(s3); }
                    ;
                    
        Label: std::u32string
                    ::= Identifier{ value = Identifier; }
                    ;
                    
        EmptyStatement: gendoc::codedom::Statement*
                    ::= ';'{ value = new EmptyStatement(); }
                    ;
                    
        ExpressionStatement: gendoc::codedom::Statement*
                    ::= (Expression ';'!){ value = new ExpressionStatement(Expression); }
                    ;
                    
        CompoundStatement(var std::unique_ptr<CompoundStatement> cs): gendoc::codedom::CompoundStatement*
                    ::= '{'{ cs.reset(new CompoundStatement()); } 
                        (Statement{ cs->Add(Statement); })* 
                        '}'!{ value = cs.release(); }
                    ;
                    
        SelectionStatement: gendoc::codedom::Statement*
                    ::= IfStatement{ value = IfStatement; }
                    |   SwitchStatement{ value = SwitchStatement; }
                    ;
                    
        IfStatement: gendoc::codedom::Statement*
                    ::= (keyword("if") '('! Condition! ')'! Statement:thenStatement! (keyword("else") Statement:elseStatement!)?)
                    {
                        value = new IfStatement(Condition, thenStatement, elseStatement);
                    }
                    ;
                    
        SwitchStatement: gendoc::codedom::Statement*
                    ::= keyword("switch") '('! Condition! ')'! Statement!
                    {
                        value = new SwitchStatement(Condition, Statement);
                    }
                    ;
                    
        IterationStatement: gendoc::codedom::Statement*
                    ::= WhileStatement{ value = WhileStatement; }
                    |   DoStatement{ value = DoStatement; }
                    |   ForStatement{ value = ForStatement; }
                    ;
                    
        WhileStatement: gendoc::codedom::Statement*
                    ::= keyword("while") '('! Condition! ')'! Statement!
                    {
                        value = new WhileStatement(Condition, Statement);
                    }
                    ;
                    
        DoStatement: gendoc::codedom::Statement*
                    ::= keyword("do") Statement! keyword("while")! '('! Expression! ')'! ';'!
                    {
                        value = new DoStatement(Statement, Expression);
                    }
                    ;
                    
        ForStatement: gendoc::codedom::Statement*
                    ::= keyword("for") '('! ForInitStatement! Condition? ';'! Expression? ')'! Statement!
                    {
                        value = new ForStatement(ForInitStatement, Condition, Expression, Statement);
                    }
                    ;
                    
        ForInitStatement: gendoc::codedom::CppObject*
                    ::= EmptyStatement{ value = EmptyStatement; }
                    |   ExpressionStatement{ value = ExpressionStatement; }
                    |   SimpleDeclaration{ value = SimpleDeclaration; }
                    ;
                    
        JumpStatement: gendoc::codedom::Statement*
                    ::= BreakStatement{ value = BreakStatement; }
                    |   ContinueStatement{ value = ContinueStatement; }
                    |   ReturnStatement{ value = ReturnStatement; }
                    |   GotoStatement{ value = GotoStatement; }
                    ;
                    
        BreakStatement: gendoc::codedom::Statement*
                    ::= keyword("break") ';'!
                    {
                        value = new BreakStatement(); 
                    }
                    ;
                    
        ContinueStatement: gendoc::codedom::Statement*
                    ::= keyword("continue") ';'!
                    {
                        value = new ContinueStatement();
                    }
                    ;
                    
        ReturnStatement: gendoc::codedom::Statement*
                    ::= keyword("return") Expression? ';'!
                    {
                        value = new ReturnStatement(Expression);
                    }
                    ;
                    
        GotoStatement: gendoc::codedom::Statement*
                    ::= keyword("goto") GotoTarget! ';'!
                    {
                        value = new GotoStatement(GotoTarget);
                    }
                    ;
                    
        GotoTarget: std::u32string
                    ::= Identifier{ value = Identifier; }
                    ;
                    
        DeclarationStatement: gendoc::codedom::Statement*
                    ::= BlockDeclaration{ value = new DeclarationStatement(BlockDeclaration); }
                    ;
                    
        Condition(var std::unique_ptr<TypeId> ti): gendoc::codedom::CppObject*
                    ::= TypeId{ ti.reset(TypeId); } Declarator '=' AssignmentExpression{ value = new ConditionWithDeclarator(ti.release(), Declarator, AssignmentExpression); }
                    |   Expression{ value = Expression; }
                    ;
                    
        TryStatement: gendoc::codedom::TryStatement*
                    ::= keyword("try") CompoundStatement! { value = new TryStatement(CompoundStatement); }
                        HandlerSeq(value)!
                    ;
        
        HandlerSeq(TryStatement* st)
                    ::= Handler!{ st->Add(Handler); } HandlerSeq(st)?
                    ;
        
        Handler: gendoc::codedom::Handler*
                    ::= keyword("catch") '('! ExceptionDeclaration! ')'! CompoundStatement!
                    {
                        value = new Handler(ExceptionDeclaration, CompoundStatement);
                    }
                    ;
                    
        ExceptionDeclaration(var std::unique_ptr<ExceptionDeclaration> ed): gendoc::codedom::ExceptionDeclaration*
                    ::= empty{ ed.reset(new ExceptionDeclaration()); }
                    (
                        TypeSpecifierSeq(ed->GetTypeId())
                        (   Declarator{ ed->GetTypeId()->Declarator() = Declarator; }
                        |   AbstractDeclarator{ ed->GetTypeId()->Declarator() = AbstractDeclarator; }
                        |   empty
                        )
                    |   "..."{ value->CatchAll() = true; }
                    )
                    {
                        value = ed.release();
                    }
                    ;
    }
}
