// =================================
// Copyright (c) 2018 Seppo Laakko
// Distributed under the MIT license
// =================================

[cpp]#include <gendoc/code/Expression.hpp>
[cpp]#include <gendoc/code/Declaration.hpp>
[cpp]#include <gendoc/util/TextUtils.hpp>
[hpp]#include <gendoc/codedom/Declaration.hpp>
[cpp]#include <gendoc/codedom/Type.hpp>

namespace gendoc.code
{
    [cpp]using gendoc::util::Trim;
    [cpp]using namespace gendoc::codedom;

    grammar Declarator
    {
        using Expression.ConstantExpression;
        using Expression.IdExpression;
        using Expression.ExpressionList;
        using Expression.AssignmentExpression;
        using Declaration.TypeName;
        using Declaration.TypeSpecifier;
        using Declaration.CVQualifier;

        InitDeclaratorList(var std::unique_ptr<InitDeclaratorList> idl): gendoc::codedom::InitDeclaratorList*
                        ::= empty{ idl.reset(new InitDeclaratorList()); }
                            (InitDeclarator{ idl->Add(InitDeclarator); } % ','){ value = idl.release(); }
                        ;

        InitDeclarator: gendoc::codedom::InitDeclarator*
                        ::= (Declarator Initializer?)
                        {
                            value = new InitDeclarator(Declarator, Initializer);
                        }
                        ;

        Declarator: std::u32string
                        ::=
                        (   DirectDeclarator
                        |   PtrOperator Declarator
                        )
                        {
                            value = Trim(std::u32string(matchBegin, matchEnd));
                        }
                        ;

        DirectDeclarator(var std::unique_ptr<CppObject> o)
                        ::= DeclaratorId
                        (
                            '[' (ConstantExpression{ o.reset(ConstantExpression); })? ']'
                            '(' Declarator ')'
                        )*
                        ;

        DeclaratorId(var std::unique_ptr<CppObject> o)
                        ::= IdExpression{ o.reset(IdExpression); }
                        |   TypeName{ o.reset(TypeName); }
                        ;

        TypeId(var std::unique_ptr<TypeId> ti): gendoc::codedom::TypeId*
                        ::= empty{ ti.reset(new TypeId()); }
                        (
                            (TypeSpecifierSeq(ti.get()) | TypeName{ ti->Add(TypeName); })
                            (AbstractDeclarator{ ti->Declarator() = AbstractDeclarator; })?
                        ){ value = ti.release(); }
                        ;

        Type(var std::unique_ptr<Type> t): gendoc::codedom::Type*
                        ::= empty{ t.reset(new Type()); }
                            (   (TypeSpecifier{ t->Add(TypeSpecifier); })+
                            |   TypeName{ t->Add(TypeName); }
                            ){ value = t.release(); }
                        ;

        TypeSpecifierSeq(gendoc::codedom::TypeId* typeId)
                        ::= (TypeSpecifier{ typeId->Add(TypeSpecifier); })+
                        ;

        AbstractDeclarator: std::u32string
                        ::=
                        (   PtrOperator AbstractDeclarator?
                        |   DirectAbstractDeclarator
                        )
                        {
                            value = Trim(std::u32string(matchBegin, matchEnd));
                        }
                        ;

        DirectAbstractDeclarator(var std::unique_ptr<CppObject> o)
                        ::=
                        (   '[' (ConstantExpression{ o.reset(ConstantExpression); })? ']'
                        |   '(' AbstractDeclarator ')'
                        )+
                        ;

        PtrOperator     ::= '*' CVQualifierSeq?
                        |   '&'
                        ;

        CVQualifierSeq(var std::unique_ptr<CppObject> o)
                        ::= (CVQualifier{ o.reset(CVQualifier); })+
                        ;

        Initializer: gendoc::codedom::Initializer*
                        ::= '=' InitializerClause{ value = new Initializer(InitializerClause, GetEmptyVector()); }
                        |   ('(' ExpressionList ')'){ value = new Initializer(nullptr, ExpressionList); }
                        ;

        InitializerClause: gendoc::codedom::AssignInit*
                        ::= AssignmentExpression{  value = new AssignInit(AssignmentExpression); }
                        |   '{'{ value = new AssignInit(nullptr); } InitializerList(value) '}'
                        |   '{' '}'{ value = new AssignInit(nullptr); }
                        ;

        InitializerList(gendoc::codedom::AssignInit* init)
                        ::= InitializerClause{ init->Add(InitializerClause); } % ','
                        ;
   }
}
